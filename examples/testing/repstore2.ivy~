#lang ivy1.6

include tcp
include udp

type key
type value
ghost type txid

type request_kind = {write,read}

object request_body = {
    type t = struct {
	knd : request_kind,
	ky : key,
	vl : value
    }
}

object request = {
    type t = struct {
	tx : txid,
	bd : request_body.t
    }
}

object response_body = {
    type t = struct {
	vl : value
    }
}

object response = {
    type t = struct {
	tx : txid,
	bd : response_body.t
    }
}

module replica = {
    function store(K:key) : value
    after init {
	store(K) := 0
    }
    action exec(inp : request_body.t) returns (out:response_body.t) = {
	if inp.knd = write {
	    store(inp.ky) := inp.vl;
	}
	else if inp.knd = read {
	    out.vl := store(inp.ky);
	}
    }
}

object dest = {
    ghost type t = {prim,sec}
}

module reference = {
    instance rep : replica
    
    action create(inp : request_body.t, dst:dest.t) returns (tx : txid)
    action serialize(tx : txid)
    action commit(tx : txid,dst:dest.t)
    action eval(tx : txid) returns (res : response_body.t)

    individual next : txid
    function txs(X:txid) : request_body.t
    function txres(X:txid) : response_body.t
    relation serialized(X:txid)
    relation committed(X:txid)

    after init {
	next := 0;
	committed(X) := false;
	serialized(X) := false;
    }

    implement create {
	tx := next;
	txs(tx) := inp;
	next := next + 1;
    }

    implement serialize {
	assert 0 <= tx & tx < next;
	assert ~serialized(tx);
	assert txs(tx).knd = write;
	serialized(tx) := true;
    }
    delegate serialize

    implement commit {
	assert 0 <= tx & tx < next;
	assert ~committed(tx);
	assert txs(tx).knd = write -> serialized(tx);
	txres(tx) := rep.exec(txs(tx));
	committed(tx) := true;
    }
    delegate commit

    implement eval {
	assert committed(tx);
	res := txres(tx);
    }
    delegate eval

    interpret txid -> int
}

instance ref : reference

type client_id

type req_msg = struct {
    cid : client_id,
    req : request.t
}


module client(cid,prim_chan,sec_chan,cl_chans) = {
    
    action client_request(req : request_body.t, the_dst: dest.t)

    implement client_request {
	local m : req_msg {
	    m.cid := cid;
	    m.req.tx := ref.create(req,the_dst);
	    m.req.bd := req;
	    if the_dst = dest.prim {
	        call prim_chan.send(m);
	    } else {
	        call sec_chan.send(m);
	    }
	}
    }
}

module primary_node(port, fwd_chan, rev_chan, cl_chans) = {
    instance rep : replica

    instance req_chan : nondup_endpoint(port,req_msg)

    instance counter : unbounded_sequence
    function pending(K:key) : counter.t

    after init {
	pending(K) := 0;
    }
	
    implement req_chan.recv(inp : req_msg) {
	var rr := inp.req.bd;
        if rr.knd = read {
	    if pending(rr.ky) = 0 {
		call ref.commit(inp.req.tx,dest.prim);
		var res : response.t;
		res.tx := inp.req.tx;
		res.bd := rep.exec(rr);
		call cl_chans(inp.cid).send(res)
	    } else {
                call req_chan.send(inp);  # if cannot execute, recirculate
	    }
	} else if rr.knd = write {
	    call ref.serialize(inp.req.tx);           # this is ghost!
	    call fwd_chan.send(inp);
	    pending(rr.ky) := pending(rr.ky).next;
	    var res := rep.exec(inp.req.bd);
	} 	    
    }

    implement rev_chan.recv(inp : req_msg) {
	var rr := inp.req.bd;
	if rr.knd = write {
	    pending(rr.ky) := pending(rr.ky).prev;
	}
    }
}

module secondary_node(port, fwd_chan, rev_chan, cl_chans) = {
    instance rep : replica

    instance req_chan : nondup_endpoint(port,req_msg)
	
    implement req_chan.recv(inp : req_msg) {
	var rr := inp.req.bd;
        if rr.knd = read {
	    call ref.commit(inp.req.tx,dest.sec);
	    var res : response.t;
	    res.tx := inp.req.tx;
	    res.bd := rep.exec(rr);
	    call cl_chans(inp.cid).send(res);
	} 	    
	# ignore writes!
    }

    implement fwd_chan.recv(inp : req_msg) {
	var res : response.t;
	res.tx := inp.req.tx;
	res.bd := rep.exec(inp.req.bd);
	call cl_chans(inp.cid).send(res);
	call rev_chan.send(inp);
    }

}


instance fwd_chan : tcp_channel("localhost:44090",req_msg)
instance rev_chan : tcp_channel("localhost:44091",req_msg)

instance cl_chans : nondup_endpoint_set(client_id,44100,response.t)
instance cl(X:client_id) : client(X,prim.req_chan,sec.req_chan,cl_chans)
instance prim : primary_node(44200,fwd_chan.sndr,rev_chan.rcvr,cl_chans)
instance sec : secondary_node(44201,fwd_chan.rcvr,rev_chan.sndr,cl_chans)

object service_spec = {
    before cl_chans.send(p : client_id, m : response.t) {
	assert m.bd = ref.eval(m.tx);
    }
}

object mid_spec = {
    instance queue : unbounded_queue(txid)

    after ref.serialize(tx:txid) {
	call queue.push(tx);
    }

    before fwd_chan.rcvr.recv(inp : req_msg) {
	assert inp.req.bd = ref.txs(inp.req.tx);
	assert inp.req.tx = queue.pop;
	call ref.commit(inp.req.tx,dest.sec);
    }

    delegate fwd_chan_rcvr_recv[before] -> prim

    relation acked(X:txid)

    after init {
	acked(X) := false;
    }
    
    before rev_chan.sndr.send(inp : req_msg) {
	assert ref.committed(inp.req.tx);
	assert ~acked(inp.req.tx);
	acked(inp.req.tx) := true;
    }
    
    function req_dest(X:txid) : dest.t

    after ref.create(inp : request_body.t, dst:dest.t) returns (tx : txid) {
	req_dest(tx) := dst
    }
	
    # Reads must be commited by the server they are addressed to, while
    # all writes are committedby the secondary

    before ref.commit(tx : txid,dst:dest.t) {
	assert ref.txs(tx).knd = read -> req_dest(tx) = dst;
	assert ref.txs(tx).knd = write -> dst = dest.sec
    }


}

export cl.client_request
import cl_chans.recv

trusted isolate iso_prim = prim with cl,cl_chans,ref,service_spec,mid_spec,fwd_chan,rev_chan
trusted isolate iso_sec = sec with cl,cl_chans,ref,service_spec,mid_spec

interpret txid -> int
interpret key -> strbv[3]
interpret value -> bv[16]
interpret client_id -> bv[1]
